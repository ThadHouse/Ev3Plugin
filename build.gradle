plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin" version "2.2"
    id "com.gradle.plugin-publish" version "0.10.0"
}

// Ensure that the WPILibVersioningPlugin is setup by setting the release type, if releaseType wasn't
// already specified on the command line
if (!hasProperty('releaseType')) {
    WPILibVersion {
        releaseType = 'dev'
    }
}

ext.pubVersion = ''
if (project.hasProperty("publishVersion")) {
    pubVersion = project.publishVersion
} else {
    pubVersion = WPILibVersion.version
}

if (pubVersion == '') {
    pubVersion = '0.0.1-unknown'
}

if (project.hasProperty('localPublish')) {
    pubVersion = '424242.0.0-local'
}

group 'thad.gradle.ev3'
version pubVersion

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'edu.wpi.first:GradleRIO:2019.1.1-beta-4c'

    testCompile('org.spockframework:spock-core:1.2-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
    testCompile gradleTestKit()
}


pluginBundle {
    website = 'https://github.com/ThadHouse/Ev3Plugin'
    vcsUrl = 'https://github.com/ThadHouse/Ev3Plugin'
    description = 'This plugin provides support for building and deploying C++ EV3 Projects.'
    tags = ['groovy', 'ev3', 'cpp']
}

gradlePlugin {
    plugins {
        GradleVsCode {
            id = 'thad.gradle.ev3.Ev3Plugin'
            displayName = 'Ev3Plugin'
            implementationClass = 'thad.gradle.ev3.Ev3Plugin'
        }
    }
}

wrapper {
    gradleVersion = '5.0'
}
